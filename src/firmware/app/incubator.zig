const rp2xxx = @import("microzig").hal;
const time = rp2xxx.time;
const usb_cdc = @import("../platform/rp2040/transport/usb_cdc.zig");
const board = @import("../platform/rp2040/board/pico_wh.zig");
const status_led = @import("../platform/rp2040/drivers/status_led.zig");
const Ticker = @import("../support/ticker.zig").Ticker;
const heater = @import("../platform/rp2040/drivers/heater.zig");
const heater_control = @import("../domain/heater_control.zig");
const temp_sensor = @import("../platform/rp2040/drivers/temp_sensor.zig");
const ultrasonic = @import("../platform/rp2040/drivers/ultrasonic.zig");
const power_switch = @import("../platform/rp2040/drivers/power_switch.zig");
const PowerSwitch = @import("../domain/power_switch_control.zig").PowerSwitch;
const rotary = @import("../platform/rp2040/drivers/rotary.zig");
const rotary_control = @import("../domain/rotary_control.zig");
const Readings = @import("./readings.zig").Readings;

const Self = @This();

ticker: Ticker,
readings: *Readings,
heater_state: heater_control.HeaterState = .power_off,
power_switch_state: PowerSwitch = .{},
rotary_state: rotary_control.Rotary = .{ .last_clk = .high, .last_sw = .high },

pub fn init(pins: board.Pins, tick_interval_us: u64, readings: *Readings) !Self {
    try temp_sensor.init(pins.temp);
    temp_sensor.configure() catch |err| {
        usb_cdc.write("ds18b20 init failed: {s}\r\n", .{@errorName(err)});
    };

    ultrasonic.init(pins.ultra_sound_trigger, pins.ultra_sound_echo);

    heater.init(pins.heater);
    power_switch.init(pins.power_switch);
    rotary.init(pins.rotary_sw, pins.rotary_clk, pins.rotary_dt);

    const self = Self{ .ticker = .{ .interval_us = tick_interval_us }, .readings = readings };
    heater.set(self.heater_state) catch |err| {
        usb_cdc.write("heater init failed: {s}\r\n", .{@errorName(err)});
    };

    return self;
}

pub fn poll(self: *Self) void {
    power_switch.read(&self.power_switch_state);
    self.pollRotary();

    const now = time.get_time_since_boot().to_us();
    if (!self.ticker.ready(now)) return;

    self.runCycle(now);
}

fn pollRotary(self: *Self) void {
    const delta = self.rotary_state.update(rotary.readClk(), rotary.readDt());
    if (delta != 0) {
        self.readings.recordTarget(rotary_control.clamp(self.readings.target_temp + delta));
    }

    if (self.rotary_state.updateButton(rotary.readSw())) {
        self.readings.recordTarget(22.0);
    }
}

fn runCycle(self: *Self, now_us: u64) void {
    status_led.toggle();

    const temp = temp_sensor.read() catch |err| {
        usb_cdc.write("temp read failed: {s}\r\n", .{@errorName(err)});
        return;
    };
    usb_cdc.write("temp: {}\r\n", .{temp});
    self.readings.recordTemp(temp);

    self.decideHeaterState(temp, now_us);
    self.readings.recordHeat(switch (self.heater_state) {
        .heating => .heating,
        else => .idle,
    });
    heater.set(self.heater_state) catch |err| {
        usb_cdc.write("heater write failed: {s}\r\n", .{@errorName(err)});
        return;
    };

    usb_cdc.write("power: {s}\r\n", .{@tagName(self.power_switch_state.state)});
    usb_cdc.write("heater: {s}\r\n", .{@tagName(self.heater_state)});

    time.sleep_ms(100);

    const distance_cm = ultrasonic.measure(usb_cdc.poll) catch |err| {
        usb_cdc.write("ultrasonic read failed: {s}\r\n", .{@errorName(err)});
        self.readings.recordDistanceTimeout();
        return;
    };
    usb_cdc.write("distance_cm: {}\r\n", .{distance_cm});
    self.readings.recordDistance(distance_cm);
}

fn decideHeaterState(self: *Self, temp: f32, now_us: u64) void {
    self.heater_state = heater_control.decide(temp, self.readings.target_temp, self.power_switch_state.state, self.heater_state, now_us);
}

test "incubator turns off the heater command after the power switch is switched off" {
    var readings: Readings = .{};
    var incubator = Self{
        .ticker = .{ .interval_us = 1 },
        .readings = &readings,
    };
    incubator.power_switch_state.switchOn();

    incubator.decideHeaterState(readings.target_temp - 1, 100);
    try @import("std").testing.expectEqual(
        heater_control.HeaterState{ .heating = .{ .since_us = 100 } },
        incubator.heater_state,
    );

    incubator.power_switch_state.switchOff();
    incubator.decideHeaterState(readings.target_temp - 1, 200);
    try @import("std").testing.expectEqual(heater_control.HeaterState.power_off, incubator.heater_state);
}
